import com.android.build.gradle.tasks.SourceJarTask
import com.vanniktech.maven.publish.AndroidMultiVariantLibrary

plugins {
    id 'com.android.library'
    id 'com.vanniktech.maven.publish'
}

android {
    namespace "com.tencent.mmkv"
    defaultConfig {
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
            }
        }
        ndkVersion "28.1.13356709"
    }
    buildTypes {
        release {
            minifyEnabled false
            externalNativeBuild {
                cmake {
                    cppFlags "-fvisibility=hidden", "-funwind-tables", "-fasynchronous-unwind-tables", "-O2"
                }
            }
            consumerProguardFiles 'proguard-rules.pro'
//            debuggable true
//            jniDebuggable true
        }
        debug {
            jniDebuggable true
        }
    }
    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
            version "3.18.0+"
        }
    }
    packagingOptions {
        jniLibs {
            useLegacyPackaging true
        }
    }
    flavorDimensions = ["stl_mode"]
    productFlavors {
        DefaultCpp {
            isDefault = true
            dimension "stl_mode"
            ext.artifactIdSuffix = ''
            externalNativeBuild {
                cmake {
                    arguments = ["-DANDROID_STL=c++_static"]
                    cppFlags "-DMMKV_STL_SHARED=0"
                }
            }
        }
        StaticCpp {
            dimension "stl_mode"
            ext.artifactIdSuffix = 'static'
            externalNativeBuild {
                cmake {
                    arguments = ["-DANDROID_STL=c++_static"]
                    cppFlags "-DMMKV_STL_SHARED=0"
                }
            }
        }
        SharedCpp {
            dimension "stl_mode"
            ext.artifactIdSuffix = 'shared'
            externalNativeBuild {
                cmake {
                    arguments = ["-DANDROID_STL=c++_shared"]
                    cppFlags "-DMMKV_STL_SHARED=1"
                }
            }
        }
    }
    buildFeatures {
        buildConfig true
        prefab true
        prefabPublishing true
        aidl true
    }
    prefab {
        mmkv {
            headers "../../../Core/include"
        }
    }
}

// TODO: should be improved to avoid hardcoding the path.
androidComponents {
    onVariants(selector().withBuildType("release")) { variant ->
        def capName = variant.name.capitalize()
        def abiFilters = ['arm64-v8a', 'x86_64', 'armeabi-v7a', 'x86']

        def syncStrippedLibsToPrefab = tasks.register("syncStrippedLibsToPrefab${capName}", Copy) {
            def start = layout.buildDirectory
                    .dir("intermediates/stripped_native_libs/${variant.name}/strip${capName}DebugSymbols/out/lib")
                    .map { it.asFile }
            def end = layout.buildDirectory
                    .dir("intermediates/prefab_package/${capName}/prefab/modules/mmkv/libs")
                    .map { it.asFile }
            from(start) {
                include "**/libmmkv.so"
            }
            into end
            eachFile {
                logger.debug("lib path before: $path")
                for (abi in abiFilters) {
                    if (path.contains("${abi}/")) {
                        path = path.replace("${abi}/", "android.${abi}/")
                        break
                    }
                }
                logger.debug("lib path after: $path")
            }
        }

        tasks.whenTaskAdded {
            if (name == "externalNativeBuild${capName}") {
                finalizedBy(syncStrippedLibsToPrefab)
            }
            if (name == "strip${capName}DebugSymbols") {
                finalizedBy(syncStrippedLibsToPrefab)
            }
            if (name == "bundle${capName}Aar") {
                dependsOn(syncStrippedLibsToPrefab)
            }
        }
    }
}

mavenPublishing {
    def buildTypes = ['release'] as Set
    def variants = ['stl_mode': ['DefaultCpp', 'StaticCpp', 'SharedCpp'] as Set]
    configure(new AndroidMultiVariantLibrary(true, true, buildTypes, variants))
}

tasks.withType(SourceJarTask).configureEach {
    doFirst {
        // Fix `Cannot copy file '.../MMKV/Android/MMKV/mmkv/build/generated/aidl_source_output_dir/DefaultCppRelease/out/com/tencent/mmkv/ParcelableMMKV.java'
        //  to 'com/tencent/mmkv/ParcelableMMKV.java' because file '.../MMKV/Android/MMKV/mmkv/src/main/java/com/tencent/mmkv/ParcelableMMKV.java'
        //  has already been copied there.`
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    }
}

dependencies {
    implementation project(':mmkvannotation')

    implementation 'androidx.annotation:annotation:1.9.1'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:runner:1.6.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
}
